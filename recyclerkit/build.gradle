apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "0.7.0"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly "com.android.support:appcompat-v7:${ANDROID_SUPPORT_VERSION}"
    compileOnly "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_VERSION}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

repositories {
    mavenCentral()
}

def siteUrl = 'https://github.com/inchtime/recyclerkit-android'
def gitUrl = 'https://github.com/inchtime/recyclerkit-android.git'
def issuesUrl = 'https://github.com/inchtime/recyclerkit-android/issues'

group = "io.inchtime"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                description 'Recyclerkit for Android RecyclerView'
                name 'recyclerkit'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'evancai'
                        name 'evancai'
                        email 'evan-cai@live.cn'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    enabled = false
//    source = android.sourceSets.main.java.srcDirs
//    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'recyclerkit'
        userOrg = properties.getProperty("bintray.org")
        licenses = ['Apache-2.0']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issuesUrl
        publish = true
    }
}
